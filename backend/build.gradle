plugins {
	id 'java'
	id 'org.springframework.boot' version '3.4.1'
	id 'io.spring.dependency-management' version '1.1.7'
	id 'jacoco'
	id "com.github.spotbugs" version "6.0.6"
	id "com.diffplug.spotless" version "6.25.0"
	id "org.flywaydb.flyway" version "11.2.0"
}

spotless {
    java {
        googleJavaFormat()
        removeUnusedImports()
        trimTrailingWhitespace()
        endWithNewline()
    }
}

group = 'com.example'
version = '0.0.1-SNAPSHOT'
description = 'Upgrad Learning Platform Backend'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(21)
	}
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
	all {
		// Force jackson-databind >= 2.16.0 to mitigate CVE-2023-35116
		resolutionStrategy {
			force 'com.fasterxml.jackson.core:jackson-databind:2.18.2'
		}
	}
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-actuator'
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-validation'
	implementation 'org.springframework.boot:spring-boot-starter-security'
	implementation 'org.springframework.boot:spring-boot-starter-mail'
	implementation 'com.auth0:java-jwt:4.5.0'
	implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.8.15'
	compileOnly 'org.projectlombok:lombok'
	runtimeOnly 'org.postgresql:postgresql'
	annotationProcessor 'org.projectlombok:lombok'
	implementation 'org.flywaydb:flyway-core:11.2.0'
	implementation 'org.flywaydb:flyway-database-postgresql:11.2.0'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
	testImplementation 'org.springframework.boot:spring-boot-testcontainers'
	testImplementation 'org.testcontainers:junit-jupiter'
	testImplementation 'org.testcontainers:postgresql:1.21.0'
	testImplementation 'com.h2database:h2'
}

tasks.named('test') {
	useJUnitPlatform()
	finalizedBy jacocoTestReport
}

tasks.named('jacocoTestReport') {
	dependsOn test
	reports {
		xml.required = true
		html.required = true
	}
}

tasks.named('jacocoTestCoverageVerification') {
	dependsOn test
	violationRules {
		rule {
			limit {
				minimum = 0.80
			}
		}
	}
}

spotbugs {
    toolVersion = '4.9.3'
    effort = 'max'
    reportLevel = 'high'
}

tasks.named('spotbugsMain') {
    reports {
        html {
            required = true
        }
        xml {
            required = true
        }
    }
}
tasks.named('check') {
	dependsOn jacocoTestCoverageVerification
}
